#version 460
#extension GL_GOOGLE_include_directive : require
#include "../common.glsl"
#include "../../../src/rendering_backend/glsl_common.h"

layout(local_size_x = 8, local_size_y = 8) in;

layout(set = 3, binding = 0, r16f) readonly uniform image2D world_space_normals_and_object_ids;
layout(set = 3, binding = 1, r16f) readonly uniform image2D motion_vectors_and_metallic_roughness;
layout(set = 3, binding = 2) uniform sampler2D depth;
layout(set = 3, binding = 3, r16f) readonly uniform image2D raytraced_shadow_and_ao_texture;
layout(set = 3, binding = 4, r16f) writeonly uniform image2D denoised_raytraced_shadow_and_ao;

layout(push_constant) uniform PushConstants { SVGFPushConstants pc; };

bool is_valid_reprojection(ivec2 prev_coords, int current_object_id, vec3 current_normal) {
	// Reject if coords is outside of display
	if(prev_coords.x < 0 || prev_coords.y < 0 ||
	   prev_coords.x >= pfd.display_size.x || prev_coords.y >= pfd.display_size.y) {
	   return false;
	}

	vec4 prev_normal_and_object_ids = imageLoad(storage_images[pc.prev_frame_normals_and_object_ids], prev_coords);

	// Reject if object IDs do not match
	int prev_object_id = int(prev_normal_and_object_ids.w);
	if(current_object_id != prev_object_id) {
		return false;
	}

	// Reject if normals aren't aligned
	// Merge metric from [Kerzner and Salvi 2014]
	vec3 prev_normal = prev_normal_and_object_ids.xyz;
	if(dot(current_normal, prev_normal) < COS_PI_4) {
		return false;
	}

	return true;
}

void main() {
	ivec2 coords = ivec2(gl_GlobalInvocationID.xy);
	vec4 current_normal_and_object_ids = imageLoad(world_space_normals_and_object_ids, coords);
	vec3 current_normal = current_normal_and_object_ids.xyz;
	int current_object_id = int(current_normal_and_object_ids.w);
	vec2 motion_vector = imageLoad(motion_vectors_and_metallic_roughness, coords).xy;
	vec4 current_shadow_and_ao = imageLoad(raytraced_shadow_and_ao_texture, coords);
	float current_shadow = current_shadow_and_ao.x;
	float current_ao = current_shadow_and_ao.y;

	// 2x2 bilinear tap filter
	vec2 prev_coords = coords - motion_vector * pfd.display_size + vec2(0.5);
	float x = fract(prev_coords.x);
	float y = fract(prev_coords.y);
	ivec2 prev_coords_aligned = ivec2(prev_coords);

	float bilinear_weights[4] = { (1 - x) * (1 - y), x * (1 - y), (1 - x) * y, x * y };
	ivec2 offsets[4] = { ivec2(0, 0), ivec2(1, 0), ivec2(0, 1), ivec2(1, 1) };

	float prev_shadow = 0.0;
	vec2 prev_shadow_moments = vec2(0.0);
	float prev_ao = 0.0;
	vec2 prev_ao_moments = vec2(0.0);
	float sum = 0.0;
	for(int i = 0; i < 4; ++i) {
		ivec2 sample_coords = prev_coords_aligned + offsets[i];
		if(is_valid_reprojection(sample_coords, current_object_id, current_normal)) {
			vec2 prev_shadow_and_ao = imageLoad(storage_images[pc.shadow_and_ao_history], sample_coords).xy;
			prev_shadow += bilinear_weights[i] * prev_shadow_and_ao.x;
			prev_ao += bilinear_weights[i] * prev_shadow_and_ao.y;

			vec4 prev_shadow_and_ao_moments = imageLoad(storage_images[pc.shadow_and_ao_moments_history], sample_coords);
			prev_shadow_moments += bilinear_weights[i] * prev_shadow_and_ao_moments.xy;
			prev_ao_moments += bilinear_weights[i] * prev_shadow_and_ao_moments.zw;
			sum += bilinear_weights[i];
		}
	}
	bool reprojection_valid = sum > 1e-6;

	// Retry with 3x3 filter if reprojection failed
	if(!reprojection_valid) {
		for(int y = -1; y <= 1; ++y) {
			for(int x = -1; x <= 1; ++x) {
				ivec2 sample_coords = prev_coords_aligned + ivec2(x, y);
				if(is_valid_reprojection(sample_coords, current_object_id, current_normal)) {
					vec2 prev_shadow_and_ao = imageLoad(storage_images[pc.shadow_and_ao_history], sample_coords).xy;
					vec4 prev_shadow_and_ao_moments = imageLoad(storage_images[pc.shadow_and_ao_moments_history], sample_coords);
					prev_shadow += prev_shadow_and_ao.x;
					prev_ao += prev_shadow_and_ao.y;
					prev_shadow_moments += prev_shadow_and_ao_moments.xy;
					prev_ao_moments += prev_shadow_and_ao_moments.zw;
					sum += 1.0;
				}	
			}
		}
		reprojection_valid = sum > 1e-6;
	}

	float shadow_luminance = current_shadow;
	vec2 shadow_moments = vec2(shadow_luminance, shadow_luminance * shadow_luminance);
	float ao_luminance = current_ao;
	vec2 ao_moments = vec2(ao_luminance, ao_luminance * ao_luminance);

	// If the reprojection is valid, integrate shadow and ao
	// and store them along with the computed variance
	if(reprojection_valid) {
		float alpha = 0.2;
		float moments_alpha = 0.2;

		prev_shadow /= sum;
		prev_shadow_moments /= sum;
		prev_ao /= sum;
		prev_ao_moments /= sum;
		shadow_moments = mix(prev_shadow_moments, shadow_moments, moments_alpha);
		ao_moments = mix(prev_ao_moments, ao_moments, moments_alpha);

		float shadow_variance = max(0.0, shadow_moments.y - shadow_moments.x * shadow_moments.x);
		float ao_variance = max(0.0, ao_moments.y - ao_moments.x * ao_moments.x);

		float integrated_shadow = mix(prev_shadow, current_shadow, alpha);
		float integrated_ao = mix(prev_ao, current_ao, alpha);

		imageStore(
			storage_images[pc.integrated_shadow_and_ao[0]],
			coords,
			vec4(integrated_shadow, integrated_ao, shadow_variance, ao_variance)
		);
	}
	else {
		float shadow_variance = max(0.0, shadow_moments.y - shadow_moments.x * shadow_moments.x);
		float ao_variance = max(0.0, ao_moments.y - ao_moments.x * ao_moments.x);
		imageStore(
			storage_images[pc.integrated_shadow_and_ao[0]],
			coords,
			vec4(current_shadow, current_ao, shadow_variance, ao_variance)
		);
	}

	// Save previous moments
	imageStore(
		storage_images[pc.shadow_and_ao_moments_history], 
		coords,
		vec4(shadow_moments, ao_moments)
	);
}
