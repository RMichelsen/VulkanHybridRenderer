#version 460
#extension GL_GOOGLE_include_directive : require
#include "../common.glsl"
#include "../../../src/rendering_backend/glsl_common.h"

layout(set = 3, binding = 0) uniform sampler2D world_space_normals_and_object_ids;
layout(set = 3, binding = 1) uniform sampler2D depth;
layout(set = 3, binding = 2, rgba16f) uniform image2D raytraced_shadow_and_ambient_occlusion;
layout(set = 3, binding = 3, rgba16f) uniform image2D raytraced_reflections;

layout(location = 0) rayPayloadEXT vec4 payload;
layout(location = 1) rayPayloadEXT vec4 reflection_payload;

void main() {
	vec2 pixel_center = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	vec2 uv = pixel_center / vec2(gl_LaunchSizeEXT.xy);
	uint rng_state = seed_thread((gl_LaunchIDEXT.y * gl_LaunchSizeEXT.y + gl_LaunchIDEXT.x) * pfd.frame_index);

	float current_depth = texture(depth, uv).x;
	if(current_depth == 0.0) {
		imageStore(raytraced_shadow_and_ambient_occlusion, ivec2(gl_LaunchIDEXT.xy), vec4(1.0, 1.0, 0.0, 1.0));
		imageStore(raytraced_reflections, ivec2(gl_LaunchIDEXT.xy), vec4(0.0));
		return;
	}

	vec3 P = get_world_space_position(current_depth, uv);
	vec3 L = -pfd.directional_light.direction.xyz;
	vec3 N = texture(world_space_normals_and_object_ids, uv).xyz;
	vec3 ray_launch_position = P + N * 0.001;

	// Trace shadow rays
	float rnd1 = random01(rng_state);
	float rnd2 = random01(rng_state);
	vec3 cone_dir = normalize(uniform_sample_cone(vec2(rnd1, rnd2), 0.999995));
	mat3x3 R = onb_from_unit_vector(L);
	
	payload = vec4(0.0, 0.0, 0.0, 0.0);
	traceRayEXT(TLAS, gl_RayFlagsOpaqueEXT | gl_RayFlagsSkipClosestHitShaderEXT | gl_RayFlagsTerminateOnFirstHitEXT, 
		0xFF, 0, 0, 0, ray_launch_position, 0.01, L /*R * cone_dir*/, 10000.0, 0);
	float shadow_payload = payload.x;

	// Trace AO rays (2spp to improve variance-guided filtering)
	float ao_payload = 0.0;
	for(int i = 0; i < 2; ++i) {
		rnd1 = random01(rng_state);
		rnd2 = random01(rng_state);
		vec3 rnd_dir = uniform_sample_cosine_weighted_hemisphere(vec2(rnd1, rnd2));
		R = onb_from_unit_vector(N);
		payload = vec4(0.0, 0.0, 0.0, 0.0);
		traceRayEXT(TLAS, gl_RayFlagsOpaqueEXT | gl_RayFlagsSkipClosestHitShaderEXT | gl_RayFlagsTerminateOnFirstHitEXT, 
			0xFF, 0, 0, 0, ray_launch_position, 0.01, R * rnd_dir, 5.0, 0);
		ao_payload += payload.x;
	}
	ao_payload /= 2.0;

	imageStore(raytraced_shadow_and_ambient_occlusion, ivec2(gl_LaunchIDEXT.xy), vec4(shadow_payload, ao_payload, 0.0, 1.0));

	// Trace reflection rays
	vec3 camera_position = vec3(pfd.camera_view_inverse[3].xyz);
	vec3 I = normalize(P - camera_position);
	vec3 reflected_dir = reflect(I, N);
	reflection_payload = vec4(0.0, 0.0, 0.0, 0.0);
	traceRayEXT(TLAS, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 1, ray_launch_position, 0.01, reflected_dir, 10000.0, 1);
	imageStore(raytraced_reflections, ivec2(gl_LaunchIDEXT.xy), reflection_payload);
}

